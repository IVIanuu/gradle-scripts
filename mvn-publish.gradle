/*
 * Copyright 2018 Manuel Wrage
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

apply from: "https://raw.githubusercontent.com/IVIanuu/gradle-scripts/master/mvn-sources.gradle"

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().substring(0, 10)
}

def isAndroid = project.hasProperty('android')

def versionToUse = Publishing.version

if (versionToUse == 'git') {
    versionToUse = gitHash()
}

group = Publishing.groupId
version = versionToUse

install {
    repositories.mavenInstaller {
        pom {
            project {
                groupId = Publishing.groupId
                artifactId = project.name

                if (isAndroid) {
                    packaging 'aar'
                }
            }
        }
        version = versionToUse
    }
    doLast() {
        println "Published: ${Publishing.groupId}:${project.name}:${versionToUse}"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            if (isAndroid) {
                pom.setPackaging('aar')
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = getProperty("bintray.user")
    key = getProperty("bintray.apikey")

    configurations = ['archives']
    publications = ['MyPublication']

    override = true

    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'ivianuu'
        licenses = ['Apache-2.0']
        vcsUrl = Publishing.vcsUrl
        websiteUrl = Publishing.vcsUrl
        issueTrackerUrl = "$Publishing.vcsUrl/issues"

        githubRepo = Publishing.vcsUrl.replace("https://github.com/", "")
        version {
            name = versionToUse
        }

        publish = true
    }
}
